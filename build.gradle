buildscript {
    ext {
        springBootVersion = '2.3.2.RELEASE'
        springCloudVersion = 'Hoxton.SR7'
        springBootManagementVersion = '1.0.9.RELEASE'
        mavenGradleAuthVersion = '2.0.1'
        mybatisPlusBootStarterVersion = '3.3.1'
        hutoolVersion = '5.2.4'

        if (project.hasProperty('profile')) {
            profile = "${profile}"
        } else {
            profile = 'pdt'
        }

        if ('pdt' == profile) {
            versionType = 'RELEASE'
            repositoryUrl = 'http://aliyun.com'
        } else {
            versionType = 'SNAPSHOT'
            repositoryUrl = 'http://aliyun.com'
        }
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${springBootManagementVersion}")
        classpath("org.hibernate.build.gradle:gradle-maven-publish-auth:${mavenGradleAuthVersion}")
    }
}

allprojects {
    group "com.zhw.sc"
    version "1.0.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'maven-publish-auth'
    apply plugin: 'io.spring.dependency-management'

    [compileJava,compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    configurations {
        all*.exclude group: 'org.springframework.boot', module:'spring-boot-starter-logging'
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
        imports { mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}" }
    }
}


